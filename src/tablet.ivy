#lang ivy1.8
# A tablet represents a set of tuples.  Depending on the tuplespace's
# replication strategy, it may either comprise a subset or the entirety of the
# space; we don't care about that at this level, though.

include collections
include order
include numbers

include utils

# C/O Ken for his help with some module-juggling to get lexord working for
# the `row` datatype.  Thanks!
instance tplord : lexord(elem,index,tuple)

class row = {
    alias t = this

    field contents : tuple
    field mark: option[server_id] 

    instance iter : order_iterator(this)
}
function(X: row < Y: row) = tplord.less(X.contents, Y.contents)

module tablet = {

    instance tupleset : ordered_set(row)

    # Lookup operations

    action get_match(tmp: tuple_template, ignore_marked: bool) returns (ret: option[tuple])
    action get_exact(tpl: tuple, ignore_marked: bool) returns (ret: option[tuple])

    # TODO: a get_all for failover?

    # Mutation operations

    # add_tuple inserts the tuple into the tablet; if the write was an
    # idempotent one, that is indicated with the return value.
    action add_tuple(tpl: tuple) returns (already_present: bool)

    # Mark and sweep operations

    # mark indicates that a given server_id has "locked" a given tuple to later
    # be removed.
    action mark(tpl: tuple, marker: server_id) returns (ok: bool)

    # sweep physically removes the tuple from the store; the removal protocol
    # necessitates that the removing server_id previously was marked for removal.
    # TODO: in a 2PC protocol, can we ever have ok=false?
    action sweep(tpl: tuple, marker: server_id) returns (ok: bool)

    # unmark cancels a marks received from some server_id on a tuple, and unmark_all
    # unmarks all tuples for a given server_id.  When a new view is received, if any
    # nodes have gone down that have marked tuples, we need to ensure those are
    # rolled back.  We also use these to undo the first phase of the two-phase
    # remove protocol if a conflict is found.
    action unmark(marker: server_id, tpl: tuple) returns (ok: bool)
    action unmark_all(marker: server_id)

    implementation {

        implement get_exact {
            debug "tablet.get_exact" with self=self, tpl=tpl, ignore_marked=ignore_marked;
            
            var key : row;
            key.contents := tpl;

            if tupleset.member(key) {
                ret := option[tuple].just(tpl);
            } else {
                ret := option[tuple].empty;
            }
        }

        implement get_match {
            debug "tablet.get_match" with self=self, tmp=tmp;

            ret := option[tuple].empty;

            # Build up a prefix value containing the first k concrete
            # elements of the tuple.
            var prefix : tuple;
            var idx : index := tmp.begin;
            var ok := true;
            while ok & idx < tmp.end {
                if tmp.get(idx).is_empty {
                    ok := false;
                } else {
                    prefix := prefix.append(tmp.get(idx).contents);
                }
                idx := idx.next;
            }
            if tmp.size > 0 & prefix.size = tmp.size {
                # If all the components of the template have values, then we
                # can trivially turn this into a tuple and then call
                # get_exact() for a direct lookup.
                ret := get_exact(prefix, ignore_marked);
            } else {
                var e := tupleset.end();

                # Here, we're using the property that lexiographically a
                # tuple's prefix is always less than the tuple itself.  So, we
                # can use the prefix's lowest upper bound as a starting point
                # to begin a search through the tablet.
                var found := false;
                var key : row;
                key.contents := prefix;
                var it := tupleset.lub(row.iter.create(key));

                while ~found & it ~= e {
                    var row := it.value();
                    var candidate := row.contents;
                    if row.mark.is_empty() & tplchk(candidate, tmp) {
                        found := true;
                        ret := option[tuple].just(candidate)
                    }
                    it := tupleset.next(it);
                }
            }
        }


        implement add_tuple {
            debug "tablet.add_tuple" with self=self, tpl=tpl;

            var r : row;
            r.contents := tpl;
            r.mark := option[server_id].empty;

            already_present := tupleset.member(r);
            if ~already_present {
                tupleset.insert(r);
            }
        }

        implement mark {
            debug "tablet.mark" with self=self, tpl=tpl, marker=marker;
            
            ok := false;

            var key : row;
            key.contents := tpl;

            if tupleset.member(key) {
                var it := tupleset.lub(row.iter.create(key));
                #var to_be_marked := tupleset.lub(row.iter.create(key)).value();
                var to_be_marked := it.value();

                if to_be_marked.mark.is_empty() {
                    # We're good to claim this row as ours to mark.
                    to_be_marked.mark := option[server_id].just(marker);
                    ok := true;

                    # Iterators return values by value, not by pointer; argh!!
                    tupleset.erase(it, tupleset.next(it));
                    tupleset.insert(to_be_marked);
                }
            }

            # 2) a concurrent remover has already marked this server_id for removal.
            # In either case, the caller needs to retry.
        }

        implement sweep {
            debug "tablet.sweep" with self=self, tpl=tpl, marker=marker;
            ok := false;

            var key : row;
            key.contents := tpl;
            assert tupleset.member(key);

            var it := tupleset.lub(row.iter.create(key));
            var r := it.value();
            
            ok := ~r.mark.is_empty & r.mark.contents=marker;
            #require ok;
            tupleset.erase(it, tupleset.next(it));
        }

        implement unmark {
            debug "tablet.unmark" with self=self, marker=marker;

            ok := false;

            var key : row;
            key.contents := tpl;
            if tupleset.member(key) {
                var it := tupleset.lub(row.iter.create(key));
                var r := it.value();

                ok := ~r.mark.is_empty & r.mark.contents = marker;
                if ok {
                    # We're good to claim this row as ours to mark.
                    r.mark := option[server_id].empty;
                    ok := true;

                    # Iterators return values by value, not by pointer; argh!!
                    tupleset.erase(it, tupleset.next(it));
                    tupleset.insert(r);
                }
            }
        }

        implement unmark_all {
            debug "tablet.unmark_all" with self=self, marker=marker;
            var it := tupleset.begin();
            var e := tupleset.end();

            # We could avoid a full table scan by storing per-server_id delete
            # lists, but, this is fine for a prototype.
            while it ~= e {
                var tpl := it.value();
                if ~tpl.mark.is_empty() & tpl.mark.contents() = marker {
                    tpl.mark := option[server_id].empty;

                    # Iterators return values by value, not by pointer; argh!!
                    tupleset.erase(it, tupleset.next(it));
                    tupleset.insert(tpl);


                    it := tupleset.lub(row.iter.create(tpl));
                }
                it := tupleset.next(it);
            }
        }
    }

    specification {
        before sweep {
            # Nobody must have gotten in to sweep out the server_id before us.
            var key : row;
            key.contents := tpl;
            require tupleset.member(key);

            var it := tupleset.lub(row.iter.create(key));
            require it ~= tupleset.end();
            var r := it.value();

            # The row must be marked by the right server_id for sweeping.
            require ~r.mark.is_empty;
            require r.mark.contents = marker;
        }
    }
}
